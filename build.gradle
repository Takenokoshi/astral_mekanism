buildscript {
    repositories {
        maven {
            name = "SpongePowered"
            url = "https://repo.spongepowered.org/maven"
        }

        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}
configurations {
    datagenImplementation.extendsFrom implementation
    datagenRuntimeOnly.extendsFrom runtimeOnly
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
sourceSets {
    datagen {
        java {
            srcDir "src/datagen/main/java"
        }
        resources {
            srcDir "src/datagen/main/resources"
        }

        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: minecraft_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }
        data {
            workingDirectory project.file("run-data")

            source sourceSets.datagen

            args '--mod', mod_id,
                 '--all',
                 '--output', file('src/generated/resources/'),
                 '--existing', file('src/main/resources/')
        }
    }
}
mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Forge"
        url = "https://maven.minecraftforge.net"
    }

    maven {
        name = "Modmaven"
        url = "https://modmaven.dev/"
        content {
            includeGroup("mekanism")
        }
    }

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        url 'https://repo.spongepowered.org/maven'
    }

    maven {
        name = "Progwml6 maven"
        url = "https://maven.blamejared.com/"
    }

    mavenCentral()
}


dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("mekanism:Mekanism:1.20.1-10.4.16.80")
    implementation fg.deobf("curse.maven:mekanism-generators-268566:6552914")
    implementation fg.deobf("curse.maven:applied-energistics-2-223794:6852919")
    implementation fg.deobf("curse.maven:applied-mekanistics-574300:4842281")
    implementation fg.deobf("curse.maven:botany-pots-353928:6348581")
    implementation fg.deobf("curse.maven:bookshelf-228525:5423987")
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    compileOnly "org.spongepowered:mixin:0.8.5"
    compileOnly fg.deobf("curse.maven:jei-238222:6075247")
    runtimeOnly fg.deobf("curse.maven:jei-238222:6075247")

}
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
        "-AoutRefMap=astral_mekanism.refmap.json"
    ]
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
